\ ga 715crystal-test.aforth --port /dev/ttyUSB0

\ Software-defined crystal! find resonant frequency of a
\ watch crystal and maintain oscillation.

\ Requires a 32.768 khz watch crystal from 715.17 to gnd

\ node 715 is a good node for driving the crystal as
\ the timing pulses can be read from the shared pins
\ by nodes 709, 713, and 717

\ counter.ga provides an example of monitoring the
\ timing pulses from node 709

\ From colorforth block 980

node 715

: -osc ( kn-f )
\ -osc tries exciting the crystal with n cycles of
\ period k returning nonzero if it didn't come back
\ high after last cycle
  io b! for
  0x30000 !b dup .. 2/ dup for unext
  0x20000 !b .. over 1 and .. + for unext next
  dup or !b dup 30000 for
  drop @b - -while next ;
  then dup or pop drop ;
: main
: clang
\ clang searches for resonant frequency over a reasonable range.
\  Initially we use 5000 cycles and may be able to shorten this.
\  When we find resonance, falls thru into 'prep' which sets up
\  registers and finally we camp in 'run' which is the low power,
\  low duty cycle oscillator
  12895 ( 12700) 200 for dup 5000 -osc while
  drop 1 . + next clang ; then
: prep 0 0x20000 0x800 0x30800 0 0x20000 0x800 0x30800
   dup up a! drop
: run !b !b @ drop run ;

\ : try ( test code for finding resonance)
\   dup 5000 -osc over 1 . + ;
\
\ do not connect any kind of conventional probe to the crystal;
\ this oscillator
\ will not work if you load it down even that much
\  => successfully tested with the Agilent MSO-X 2014A oscilloscope
